plugins {
	id 'java'
	id 'eclipse'
	id "maven"
	id "maven-publish"
	id 'net.saliman.cobertura' version '2.3.0'
	id "com.jfrog.bintray" version "1.6"
	id 'co.riiid.gradle' version '0.4.2'
	id 'synapticloop.documentr' version '2.7.2'
	id "synapticloop.copyrightr" version "1.1.2"
}

group = 'synapticloop'
archivesBaseName = 'scaleway-java-api'
description = """A java api for the scaleway service"""

version = '0.10.2'

sourceCompatibility = 1.7
targetCompatibility = 1.7



repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
}

dependencies {
	runtime 'org.apache.httpcomponents:httpclient:4.5.1'
	runtime 'commons-io:commons-io:2.4'
	runtime 'com.fasterxml.jackson.core:jackson-databind:2.8.5'
	runtime 'org.slf4j:slf4j-api:1.7.13'

	compile 'org.apache.httpcomponents:httpclient:4.5.1'
	compile 'commons-io:commons-io:2.4'
	compile 'com.fasterxml.jackson.core:jackson-databind:2.8.5'
	compile 'org.slf4j:slf4j-api:1.7.13'

	testCompile 'junit:junit:4.12'
	testCompile 'org.apache.logging.log4j:log4j-slf4j-impl:2.5'
	testCompile 'org.apache.logging.log4j:log4j-core:2.5'

	testRuntime 'junit:junit:4.12'
	testRuntime 'org.apache.logging.log4j:log4j-slf4j-impl:2.5'
	testRuntime 'org.apache.logging.log4j:log4j-core:2.5'
}

configurations.all {
	resolutionStrategy {
		failOnVersionConflict()
		force 'org.slf4j:slf4j-api:1.7.13',
					'xerces:xmlParserAPIs:2.6.2',
					'xerces:xercesImpl:2.6.2',
					'xml-apis:xml-apis:1.0.b2'

		eachDependency {
			DependencyResolveDetails details ->
				if (details.requested.group == 'junit') {
					details.useVersion '4.12'
				}
		}
	}
}

copyrightr {
	dryRun = false;
}
/*
 * COBERTURA
 */

cobertura {
	coverageFormats = [ 'html', 'xml']
}

test {
	include '**/*Test.class' 
	maxParallelForks = 1
	forkEvery = 50 
}


/*
 *
 * Publish to bintray
 *
 */


def javaApiUrl = 'http://docs.oracle.com/javase/1.7.0/docs/api/'
def groovyApiUrl = 'http://groovy.codehaus.org/gapi/'

tasks.withType(Javadoc) {
	options.links(javaApiUrl, groovyApiUrl)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

publishing {
	publications {
		Synapticloop(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar

			groupId 'synapticloop'
			artifactId 'scaleway-java-api'

			pom.withXml {
				configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.each { dep ->
					asNode().dependencies[0].dependency.find {
						it.artifactId[0].text() == dep.moduleName &&
								it.groupId[0].text() == dep.moduleGroup
					}.scope[0].value = 'compile'
				}
			}
		}
	}
}

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_PASSWORD')

	publications = [ 'Synapticloop' ]

	publish = true

	pkg {
		repo = 'maven'
		name = 'scaleway-java-api'
	}

}

github {
	owner = "synapticloopltd"
	repo = archivesBaseName
	if(System.getenv('GITHUB_TOKEN')) {
		token = System.getenv('GITHUB_TOKEN')
	}
	tagName = version
	name = version
	assets = [
		'build/libs/' + archivesBaseName + '-' + version + '.jar'
	]
}
